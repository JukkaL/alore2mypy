module pygen

import nodes
import reflect
import re


-- Extract and convert whitespace/comments before/after a node.


const SepMap = Map(
  VarDef : ['visibility', 'type'],
  FuncDef : ['visibility', 'defTok'],
  TypeDef : ['visibility', 'type'],
  WhileStmt : ['whileTok'],
  RepeatStmt : ['repeatTok'],
  BreakStmt : ['breakTok'],
  ForStmt : ['forTok'],
  IfStmt : ['ifTok'],
  ReturnStmt : ['returnTok'],  
  SwitchStmt : ['switchTok'],
  ForStmt : ['forTok'],
  TryFinallyStmt : ['tryTok'],
  TryExceptStmt : ['tryTok'],
  RaiseStmt : ['raiseTok'],
  ExpressionStmt : [':expr'],
  
  NameExpr : ['components'],
  NameLvalue : ['lparens', 'base.components'],
  
  ParenExpr : ['lparen'],
  SelfExpr : ['selfTok'],
  CallExpr : [':callee'],
  MemberExpr : [':expr'],
  TypeApplication : [':expr'],
  
  ) as Map<Type, Array<Str>>


def GetSeparator(o)
  if o.repr == nil
    return ''
  end
  var toks = GetSeparatorToks(o)
  return GetSep(toks)
end


def GetSeparatorToks(o)
  var toks = []
  for member in SepMap.get(TypeOf(o), [])
    var t = GetMembers(o, member)
    if t is Array
      if t != []
        toks.append(t[0])
      end
    else
      toks.append(t)
    end
  end
  return toks
end


def GetMembers(o, members)
  if not members.startsWith(':')
    var r = o.repr
    for m in members.split('.')
      r = GetMember(r, m)
    end
    return r
  else
    return GetSeparatorToks(GetMember(o, members[1:]))
  end
end


private def GetSep(toks)
  for t in toks
    if t.str != ''
      return Process(t.pre)
    end
  end
  return ''
end


private def Process(pre)
  pre = Subst(pre, '--([^\n\r]*)', '#\1')
  pre = Subst(pre, '^ +', '')
  pre = Subst(pre, ' +([\n#])', '\1')
  pre = Subst(pre, ' +$', '')
  return pre
end


const AfterCommentMap = Map(
  VarDef : 'br',
  FuncDef : 'defBr',
  TypeDef : 'headerBr',
  AssignmentStmt : 'br',
  OperatorAssignmentStmt : 'br',
  ExpressionStmt : 'br',
  ReturnStmt : 'br')

  
def GetAfterComment(o)
  var m = AfterCommentMap.get(TypeOf(o), nil)
  if o.repr == nil or m == nil
    return ''
  end
  var cc = GetMember(o.repr, m).pre
  cc = cc.replace('--', '#')
  return cc
end